;redcode-94
;name Insight v1.0
;author Brant D. Thomsen
;strategy Stone/imp-spiral
;strategy Uses A-field indirection
;strategy Submitted: @date@
;assert CORESIZE == 8000

step	equ	3039
init	equ	step+1
gate	equ	-12
impstep equ	2667

cdist	equ	8000 / 22

	dat.A	#1, #1	; Large decoy to slow DJN streams,
	dat.A	#1, 1	; while still remaining unique
	dat.A	#1, @1	; in case of CMP scanners.
	dat.A	#1, <1
	dat.A	#1, >1
	dat.A	#1, *1
	dat.A	#1, {1
	dat.A	#1, }1
	dat.A	1, #1
	dat.A	1, 1
	dat.A	1, @1
	dat.A	1, <1
	dat.A	1, >1
	dat.A	1, *1
	dat.A	1, {1
	dat.A	1, }1
	dat.A	@1, #1
	dat.A	@1, 1
	dat.A	@1, @1
	dat.A	@1, <1
	dat.A	@1, >1
	dat.A	@1, *1
	dat.A	@1, {1
	dat.A	@1, }1
	dat.A	<1, #1
	dat.A	<1, 1
	dat.A	<1, @1
	dat.A	<1, <1
	dat.A	<1, >1
	dat.A	<1, *1
	dat.A	<1, {1
	dat.A	<1, }1
	dat.A	>1, #1
	dat.A	>1, 1
	dat.A	>1, @1
	dat.A	>1, <1
	dat.A	>1, >1
	dat.A	>1, *1
	dat.A	>1, {1
	dat.A	>1, }1
	dat.A	*1, #1
	dat.A	*1, 1
	dat.A	*1, @1
	dat.A	*1, <1
	dat.A	*1, >1
	dat.A	*1, *1
	dat.A	*1, {1
	dat.A	*1, }1
	dat.A	{1, #1
	dat.A	{1, 1
	dat.A	{1, @1
	dat.A	{1, <1
	dat.A	{1, >1
	dat.A	{1, *1
	dat.A	{1, {1
	dat.A	{1, }1
	dat.A	}1, #1
	dat.A	}1, 1
	dat.A	}1, @1
	dat.A	}1, <1
	dat.A	}1, >1
	dat.BA	1, >1
	dat.BA	1, *1
	dat.BA	1, {1
	dat.BA	1, }1

stone	spl	#gate, <gate
loop	mov	data, *init	; "data" placed here by last hit,
				; to create a "perfect" imp-gate.
	add	#step, loop
	djn.F	loop, @loop	; Last hit is actually here
data	dat.F	<gate, #0

start	mov	stone + 4, @boot
	mov	stone + 3, <boot
	mov	stone + 2, <boot
	mov	stone + 1, <boot
	mov	stone + 0, <boot

;Binary imp launch
; 3 point, 10 processes -- generated by bimp

spiral	spl	16,			{cdist * 1	; Use the B-field of
	spl	8,			{cdist * 2	; the SPL and JMP
	spl	4,			{cdist * 3	; instructions to
	spl	2,			{cdist * 4	; decrement locations
	jmp	imp + 0,		{cdist * 5	; in hopes of an early
	jmp	imp + impstep,		{cdist * 6	; advantage
	spl	2,			{cdist * 7
	jmp	imp + 2 * impstep,	{cdist * 8
	jmp	imp + 1,		{cdist * 9
	spl	4,			{cdist * 10
	spl	2,			{cdist * 11
	jmp	imp + impstep + 1,	{cdist * 12
	jmp	imp + 2 * impstep + 1,	{cdist * 13
	spl	2,			{cdist * 14
	jmp	imp + 2,		{cdist * 15
	jmp	imp + impstep + 2,	{cdist * 16

; Jump to stone
	spl	@boot,			{cdist * 17
	spl	@boot,			{cdist * 18

	spl	2,			{cdist * 19
	jmp	imp + 2 * impstep + 2,	{cdist * 20
	jmp	imp + 3,		{cdist * 21

imp	mov.I	#-100, impstep

	dat.F	#1, #1
	dat.F	1, 1

boot	dat.F	#0, #2550	; Bootstrapping distance
				; Will be overwritten by the imp-spiral

	end	start
